module tpeq.

import cast.

TpEq : ★ ➔ ★ ➔ ★ = λ A: ★. λ B: ★. ι _: Cast ·A ·B. Cast ·B ·A.

intrTpEq : ∀ A: ★. ∀ B: ★. Cast ·A ·B ➾ Cast ·B ·A ➾ TpEq ·A ·B
  = Λ A. Λ B. Λ c1. Λ c2. [ [ elimCast -c1 , β] , [ elimCast -c2 , β ] ].

elimTpEq1 : ∀ A: ★. ∀ B: ★. TpEq ·A ·B ➾ A ➔ B
  = Λ A. Λ B. Λ teq. λ a. elimCast -teq.1 a.

elimTpEq2 : ∀ A: ★. ∀ B: ★. TpEq ·A ·B ➾ B ➔ A
  = Λ A. Λ B. Λ teq. λ b. elimCast -teq.2 b.

tpEqRefl : ∀ A: ★. TpEq ·A ·A
= Λ A. intrTpEq -(castRefl ·A) -(castRefl ·A).

tpEqSym : ∀ A: ★. ∀ B: ★. TpEq ·A ·B ➾ TpEq ·B ·A
= Λ A. Λ B. Λ eq.
  intrTpEq
    -eq.2
    -eq.1 .

tpEqTrans
: ∀ A: ★. ∀ B: ★. ∀ C: ★.
  TpEq ·A ·B ➾ TpEq ·B ·C ➾ TpEq ·A ·C
= Λ A. Λ B. Λ C. Λ eq1. Λ eq2.
  intrTpEq
    -(castTrans -eq1.1 -eq2.1)
    -(castTrans -eq2.2 -eq1.2) .
